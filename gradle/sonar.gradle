apply plugin: 'org.sonarqube'

def isTarget = 'master'.equals(System.env.TRAVIS_BRANCH) || 'release-3.1'.equals(System.env.TRAVIS_BRANCH)

// for security reasons travis secure variables are defined when build is
// initiated by a pull request from same repository only
// so pull requests from external repositories can not be analysed by sonar
def isSecure = 'true'.equals(System.env.TRAVIS_SECURE_ENV_VARS)

if (!isSecure || !isTarget) {
    rootProject.tasks['sonarqube'].setEnabled(false)
    return
}

def isPush = 'push'.equals(System.env.TRAVIS_EVENT_TYPE);
def isPullRequest = 'pull_request'.equals(System.env.TRAVIS_EVENT_TYPE);

rootProject.tasks['sonarqube'].setEnabled(isPush || isPullRequest)

sonarqube {
    properties {
        property 'sonar.host.url', System.env.SONAR_HOST_URL
        property 'sonar.login', System.env.SONAR_LOGIN
        property 'sonar.projectKey', 'com.github.spotbugs.spotbugs'
        property 'sonar.projectName', 'SpotBugs'
        property 'sonar.projectVersion', rootProject.version
        property 'sonar.organization', 'spotbugs'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${rootProject.buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    }
}

if (isPullRequest) {
    sonarqube {
        properties {
            // https://docs.sonarqube.org/7.6/analysis/pull-request/
            // https://github.com/travis-ci/travis-build/blob/6af6dd723bd6b364dc29ad74af7f22438a16494d/lib/travis/build/addons/sonarcloud.rb#L123-L127
            property 'sonar.pullrequest.key', System.env.TRAVIS_PULL_REQUEST
            property 'sonar.pullrequest.branch', System.env.TRAVIS_PULL_REQUEST_SLUG
            property 'sonar.pullrequest.base', System.env.TRAVIS_BRANCH
            property 'sonar.pullrequest.provider', 'GitHub'
            property 'sonar.pullrequest.github.repository', System.env.TRAVIS_REPO_SLUG
        }
    }
} else {
    sonarqube {
        properties {
            // https://docs.sonarqube.org/display/PLUG/Branch+Plugin
            property 'sonar.branch.name', System.env.TRAVIS_BRANCH
        }
    }
}

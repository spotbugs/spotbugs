apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
tasks.named('compileJava', JavaCompile).configure {
    options.release = 8
}

// make the build reproducible https://reproducible-builds.org/
tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
  dirMode = 0775
  fileMode = 0664
}

def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsTask = classLoader.findLoadedClass( 'com.github.spotbugs.snom.SpotBugsTask' )
spotbugs {
    effort = 'max'
    reportLevel = 'high'
    maxHeapSize = '1g'
}
tasks.withType(SpotBugsTask).configureEach {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
tasks.named('compileJava', JavaCompile).configure {
    options.release = 11
}

// make the build reproducible https://reproducible-builds.org/
tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
  dirPermissions {
    unix (0775)
  }
  filePermissions {
    unix (0664)
  }
}

spotbugs {
    effort = Effort.MAX
    reportLevel = Confidence.HIGH
    maxHeapSize = '1g'
}
tasks.withType(SpotBugsTask).configureEach {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

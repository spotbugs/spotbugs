apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
tasks.named('compileJava', JavaCompile).configure {
    options.release = 11
}

// make the build reproducible https://reproducible-builds.org/
tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
  dirPermissions{
    unix (0775)
  }
  filePermissions{
    unix (0664)
  }
}

def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsTask = classLoader.findLoadedClass( 'com.github.spotbugs.snom.SpotBugsTask' )
def SpotBugsEffort = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Effort' )
def SpotBugsConfidence = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Confidence' )
spotbugs {
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.HIGH
    maxHeapSize = '1g'
}
tasks.withType(SpotBugsTask).configureEach {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
tasks.named('compileJava', JavaCompile).configure {
    options.release = 11
}

// make the build reproducible https://reproducible-builds.org/
tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
  dirPermissions {
    unix (0775)
  }
  filePermissions {
    unix (0664)
  }
}

afterEvaluate {
  if (plugins.hasPlugin('com.github.spotbugs')) {
    // Configure SpotBugs tasks using string values and string-based task type
    tasks.withType('com.github.spotbugs.snom.SpotBugsTask').configureEach {
      effort = 'max'
      reportLevel = 'high'
      maxHeapSize = '1g'
      reports {
        html {
          required = true
          stylesheet = 'fancy-hist.xsl'
         }
      }
    }
  }
}

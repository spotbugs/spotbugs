plugins {
  id "com.github.spotbugs" version "1.4"
}

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/maven.gradle"

dependencies {
  compile project(':spotbugs')
  compile 'org.apache.ant:ant:1.9.4'
}

javadoc {
  options.with {
    memberLevel = JavadocMemberLevel.PUBLIC
    docTitle = 'SpotBugs Ant Task Documentation'
  }
}

jar {
  // To keep backward compatibility, delete version number from jar name
  archiveName "${baseName}.${extension}"
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'SpotBugs Ant Task'
        description 'Ant Task to run SpotBugs'
      }
    }
  }
}

// Module name should be reverse-DNS (com.github.spotbugs) just like package name
// http://blog.joda.org/2017/04/java-se-9-jpms-module-naming.html
ext.moduleName = 'com.github.spotbugs.ant'
apply from: "$rootDir/gradle/jigsaw.gradle"

spotbugs {
  effort = "max"
  reportLevel = "high"
  toolVersion '3.1.0-RC6'
}

sourceSets {
  main {
    java {
      srcDirs = ['src/java', 'src/fakeAnnotations', 'src/fakeLibraries']
    }
  }
}

dependencies {
  // TODO : Some of these can be extracted to actual dependencies
  implementation fileTree(dir: 'lib', include: '*.jar')

  implementation 'com.google.code.gson:gson:2.8.6'
  implementation 'com.google.guava:guava:30.0-jre'
  implementation 'com.google.inject:guice:4.2.3'
  implementation 'com.google.inject.extensions:guice-assistedinject:4.2.3'
  implementation 'com.google.inject.extensions:guice-servlet:4.2.3'
  implementation 'com.google.truth:truth:1.0.1'
  implementation 'joda-time:joda-time:2.10.6'
  api 'net.jcip:jcip-annotations:1.0'
  implementation 'org.springframework:spring-core:5.2.9.RELEASE'
  compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'org.checkerframework:checker-qual:3.7.0'

  implementation 'junit:junit:4.13.1'
  implementation 'org.testng:testng:7.3.0'

  implementation project(':spotbugs')
  api project(':spotbugs-annotations')
}

tasks.withType(JavaCompile).all {
  options.compilerArgs << '-Xlint:none'
  options.encoding = 'UTF-8'
  if (it.name == 'classesJava11') {
    options.compilerArgs.addAll(['--release', '11'])
  } else if (it.name != 'compileJava') {
    options.compilerArgs.addAll(['--release', '8'])
  }
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
javadoc {
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)

    // TODO remove command and warning below if the doclint can be properly configured
    options.addStringOption('Xmaxwarns', '3')
  }
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

task classesJava8(type:JavaCompile) {
  destinationDir = file("$buildDir/classes/java/java8")
  classpath = sourceSets.main.compileClasspath
  source = file('src/java8')
}

task classesJava11(type:JavaCompile) {
  destinationDir = file("$buildDir/classes/java/java11")
  classpath = sourceSets.main.compileClasspath
  source = file('src/java11')
}

tasks['classes'].dependsOn classesJava8
tasks['classes'].dependsOn classesJava11

sonarqube {
  // this project should not be analyzed with sonarqube
  // as it is test data, not real code
  skipProject true
}

spotbugs {
  ignoreFailures = true
}

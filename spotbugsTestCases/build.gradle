sourceSets {
  main {
    java {
      // TODO : Ant currently ignores src/java7... we can probably move all files directly to src/java
      srcDirs = ['src/java', 'src/java7', 'src/fakeAnnotations', 'src/fakeLibraries']
    }
  }
}

dependencies {
  // TODO : Some of these can be extracted to actual dependencies
  compile fileTree(dir: 'lib', include: '*.jar')
  compile project(':spotbugs')
  compile project(':spotbugs-annotations')
}

task classesJava7(type:JavaCompile) {
  exclude '**/*jdk8.java'
  exclude '**/lambdas/**'
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
  destinationDir = file("$buildDir/classes-jdk7")
  classpath = sourceSets.main.compileClasspath
  source = sourceSets.main.java
}

tasks.withType(JavaCompile).all {
  options.compilerArgs << '-Xlint:none'
}

tasks['build'].dependsOn classesJava7

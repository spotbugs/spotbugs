sourceSets {
  main {
    java {
      // TODO : Ant currently ignores src/java7... we can probably move all files directly to src/java
      srcDirs = ['src/java', 'src/java7', 'src/fakeAnnotations', 'src/fakeLibraries']
    }
  }
}

dependencies {
  // TODO : Some of these can be extracted to actual dependencies
  compile fileTree(dir: 'lib', include: '*.jar')

  compile 'com.google.code.gson:gson:2.8.1'
  compile 'com.google.inject:guice:4.1.0'
  compile 'com.google.inject.extensions:guice-assistedinject:4.1.0'
  compile 'com.google.inject.extensions:guice-servlet:4.1.0'
  compile 'com.google.truth:truth:0.36'
  compile 'joda-time:joda-time:2.9.9'
  compile 'net.jcip:jcip-annotations:1.0'
  compile 'org.springframework:spring-core:4.3.8.RELEASE'
  compile 'org.slf4j:slf4j-api:1.7.25'

  compile 'junit:junit:4.12'
  compile 'org.testng:testng:6.11'

  compile project(':spotbugs')
  compile project(':spotbugs-annotations')
}

tasks.withType(JavaCompile).all {
  options.compilerArgs << '-Xlint:none'
  options.encoding = 'UTF-8'
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
javadoc {
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)

    // TODO remove command and warning below if the doclint can be properly configured
    options.addStringOption('Xmaxwarns', '3')
  }
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

task classesJava9(type:JavaCompile) {
  sourceCompatibility = '9'
  targetCompatibility = '9'
  destinationDir = file("$buildDir/classes/java/java9")
  classpath = sourceSets.main.compileClasspath
  source = file('src/java9')
}

if (System.getProperty("java.specification.version") == "9") {
  tasks['classes'].dependsOn classesJava9
}

task classesJava7(type:JavaCompile) {
  exclude '**/*jdk8.java'
  exclude '**/lambdas/**'
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
  destinationDir = file("$buildDir/classes-jdk7")
  classpath = sourceSets.main.compileClasspath
  source = sourceSets.main.java
}

tasks['classes'].dependsOn classesJava7

sonarqube {
  // this project should not be analyzed with sonarqube
  // as it is test data, not real code
  skipProject true
}

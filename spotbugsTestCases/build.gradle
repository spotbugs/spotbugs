plugins {
  id 'groovy'
}

sourceSets {
  main {
    java {
      srcDirs = ['src/java', 'src/fakeAnnotations', 'src/fakeLibraries']
    }
    groovy {
      srcDirs = ['src/groovy']
    }
  }
}

dependencies {
  // TODO : Some of these can be extracted to actual dependencies
  implementation fileTree(dir: 'lib', include: '*.jar')

  implementation 'com.google.code.gson:gson:2.10.1'
  implementation 'com.google.guava:guava:32.1.2-jre'
  implementation libs.guice
  implementation libs.guice.assistedinject
  implementation libs.guice.servlet
  implementation 'com.google.truth:truth:1.1.5'
  implementation 'joda-time:joda-time:2.12.5'
  api 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
  implementation 'org.springframework:spring-core:6.0.12'
  compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
  implementation 'org.checkerframework:checker-qual:3.38.0'

  implementation 'org.junit.vintage:junit-vintage-engine:5.10.0'
  implementation 'org.testng:testng:7.8.0'

  implementation project(':spotbugs')
  api project(':spotbugs-annotations')
  implementation 'org.apache.groovy:groovy-all:4.0.15'
  implementation 'org.mockito:mockito-core:5.5.0'
}

tasks.withType(JavaCompile).configureEach {
  javaCompiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(17)
  }
  options.compilerArgs << '-Xlint:none'
  options.encoding = 'UTF-8'
  if (it.name == 'classesJava17') {
    options.release = 17
  } else if (it.name == 'classesJava11') {
    options.release = 11
  } else if (it.name != 'compileJava') {
    options.release = 8
  }
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
tasks.named('javadoc', Javadoc).configure {
  // This is supposed to enable everything except "missing" but doesn't work with gradle
  // See http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
  options.addBooleanOption('Xdoclint:all,-missing', true)

  // TODO remove command and warning below if the doclint can be properly configured
  options.addStringOption('Xmaxwarns', '3')
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

def classesJava8 = tasks.register('classesJava8', JavaCompile) {
  destinationDirectory.set(file("$buildDir/classes/java/java8"))
  classpath = sourceSets.main.compileClasspath
  source = file('src/java8')
}

def classesJava11 = tasks.register('classesJava11', JavaCompile) {
  destinationDirectory.set(file("$buildDir/classes/java/java11"))
  classpath = sourceSets.main.compileClasspath
  source = file('src/java11')
}

def classesJava17 = tasks.register('classesJava17', JavaCompile) {
  destinationDirectory.set(file("$buildDir/classes/java/java14"))
  classpath = sourceSets.main.compileClasspath
  source = file('src/java14')
}

tasks.named('classes').configure {
  dependsOn classesJava8
  dependsOn classesJava11
  dependsOn classesJava17
}

sonarqube {
  // this project should not be analyzed with sonarqube
  // as it is test data, not real code
  skipProject true
}

spotbugs {
  ignoreFailures = true
}

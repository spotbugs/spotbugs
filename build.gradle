import org.gradle.crypto.checksum.Checksum
import org.gradle.api.Task;
import org.gradle.api.tasks.TaskProvider;
import org.gradle.api.file.RegularFile;
import org.gradle.api.provider.Provider;
import org.gradle.api.Project;
import org.gradle.api.publish.PublishingExtension;
import org.gradle.api.publish.maven.MavenArtifact;
import org.gradle.api.tasks.util.PatternFilterable;

plugins {
  id 'org.sonarqube' version '7.0.1.6134'
  id 'org.gradle.crypto.checksum' version '1.4.0'
  id 'com.github.spotbugs' version '6.4.5'
  id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'com.github.spotbugs'
version = '4.9.10-SNAPSHOT'

apply from: "${rootDir}/gradle/java.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"

subprojects {
  apply plugin: 'constraints'
  apply from: "${rootDir}/gradle/java.gradle"
  apply from: "${rootDir}/gradle/eclipse.gradle"
  apply from: "${rootDir}/gradle/idea.gradle"
  apply from: "${rootDir}/gradle/test.gradle"
  if (!project.name.equals('spotbugsTestCases')) {
    apply from: "${rootDir}/gradle/spotless.gradle"
  }

  group = 'com.github.spotbugs'
  version = rootProject.version
}

allprojects {
  repositories {
    mavenCentral()
  }
  dependencies {
    String junitVersion = '5.14.1'
    implementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
  }
}

// https://discuss.gradle.org/t/merge-jacoco-coverage-reports-for-multiproject-setups/12100/6
tasks.register('jacocoRootReport', JacocoReport.class) {
  description = 'Merge all coverage reports before submit to SonarQube'
  Set<JacocoReport> reportTasks = project.getTasksByName('jacocoTestReport', true).minus(rootProject.jacocoTestReport)
  dependsOn reportTasks

  executionData.setFrom reportTasks.executionData
  sourceDirectories.setFrom reportTasks.sourceDirectories

  // Only enable class directories related to non-test project
  classDirectories.setFrom files(reportTasks.classDirectories).filter { File dir ->
    !dir.toString().contains('-test') && !dir.toString().contains('Test') && !dir.toString().contains('junit')
  }

  reports {
    // JaCoCo SonarQube plugin needs a XML file to parse
    // https://docs.sonarqube.org/display/PLUG/JaCoCo+Plugin
    xml.required = true
  }
}

TaskProvider<Task> createReleaseBody = tasks.register('createReleaseBody')
TaskProvider<Checksum> createChecksums = tasks.register('createChecksums', Checksum)
TaskProvider<Task> publishTask = tasks.findByName('publish')
if (publishTask == null) {
  publishTask = tasks.findByName('publishToMavenLocal')
}
Provider<RegularFile> outputFile = layout.buildDirectory.file('release.md')

// Defer artifact file collection to execution time for Gradle 9 compatibility
createChecksums.configure { Checksum checksumTask ->
  checksumTask.files.from provider {
    subprojects.collect { Project subproject ->
      if (subproject.hasProperty('publishing')) {
        PublishingExtension publishing = (PublishingExtension) subproject.publishing
        if (publishing.publications.findByName('maven')) {
          return publishing.publications.maven.artifacts.collect { MavenArtifact artifact ->
            return artifact.file
          }
        }
      }
      return []
    }.flatten().findAll { File file -> file != null && file.exists() }
  }
  if (publishTask != null) {
    checksumTask.dependsOn publishTask
  }
  subprojects.each { Project subproject ->
    checksumTask.dependsOn("${subproject.name}:jar")
  }

  dependsOn(':spotbugs:javadocJar')
  dependsOn(':spotbugs:sourcesJar')
  dependsOn(':spotbugs-annotations:javadocJar')
  dependsOn(':spotbugs-annotations:sourcesJar')
  dependsOn(':spotbugs-ant:javadocJar')
  dependsOn(':spotbugs-ant:sourcesJar')
  dependsOn(':test-harness:javadocJar')
  dependsOn(':test-harness:sourcesJar')
  dependsOn(':test-harness-core:javadocJar')
  dependsOn(':test-harness-core:sourcesJar')
  dependsOn(':test-harness-jupiter:javadocJar')
  dependsOn(':test-harness-jupiter:sourcesJar')
  dependsOn(':spotbugs:distZip')
  dependsOn(':spotbugs:distTar')
}
createReleaseBody.configure { Task releaseTask ->
  releaseTask.inputs.files fileTree(layout.buildDirectory.dir('checksums')).matching { PatternFilterable pattern ->
    pattern.include '*.sha256'
  }
  releaseTask.outputs.file outputFile
  releaseTask.dependsOn createChecksums

  String version = project.version
  doLast {
    File outputAsFile = outputFile.get().asFile
    outputAsFile.delete()
    outputAsFile << """SpotBugs ${version}

### CHANGELOG
- https://github.com/spotbugs/spotbugs/blob/${version}/CHANGELOG.md

### CHECKSUM
| file | checksum (sha256) |
| ---- | ----------------- |
"""
    fileTree(layout.buildDirectory.dir('checksums')).matching { PatternFilterable pattern ->
      pattern.include '*.sha256'
    }.sort { File a, File b -> a.name <=> b.name }.forEach { File checksumFile ->
      String name = checksumFile.name.replace('.sha256', '')
      String hash = checksumFile.text.trim()
      outputFile.get().asFile << "| ${name} | ${hash} |\n"
    }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
      snapshotRepositoryUrl.set(uri('https://central.sonatype.com/repository/maven-snapshots/'))
      username.set(findProperty('ossrhUsername') ?: '')
      password.set(findProperty('ossrhPassword') ?: '')
    }
  }
}

apply from: "${rootDir}/gradle/sonar.gradle"

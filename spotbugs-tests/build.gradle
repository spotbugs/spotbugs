apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

dependencies {
  implementation(project(':spotbugs')) {
    transitive = true
  }
  // These annotations are repackaged to spotbugs.jar, to keep backward compatibility for Ant task.
  // If they're not repackaged, Ant task will report 'java.lang.ClassNotFoundException: edu.umd.cs.findbugs.annotations.CleanupObligation'
  api project(':spotbugs-annotations')
  implementation project(':spotbugsTestCases')
  implementation project(':test-harness')

  implementation 'junit:junit:4.12'
  implementation 'org.hamcrest:hamcrest-all:1.3'
  implementation 'org.apache.ant:ant:1.9.4'
}


test {
  doFirst {
    def auxClasspath = project(':spotbugsTestCases').configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.file
    systemProperty 'AUX_CLASSPATH', auxClasspath.join(',')
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

jacocoTestReport {
  additionalSourceDirs.setFrom files(project(':spotbugs').sourceSets.main.java.srcDirs)
  additionalClassDirs.setFrom files(project(':spotbugs').sourceSets.main.output.classesDirs)
}

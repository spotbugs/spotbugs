plugins {
  id "com.github.spotbugs" version "1.4"
}

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/maven.gradle"

dependencies {
  compile 'com.google.code.findbugs:jsr305:3.0.1'
}

def manifestSpec = manifest {
  attributes 'Bundle-Name': 'spotbugs-annotations',
               'Bundle-SymbolicName': 'spotbugs-annotations',
               'Bundle-Version': project.version.replace('-', '.'),
               'Export-Package': 'edu.umd.cs.findbugs.annotations',
               'Bundle-RequiredExecutionEnvironment': 'J2SE-1.5',
               'Bundle-ManifestVersion': '2'
}

task updateManifest {
    // write manifests
    manifestSpec.writeTo("$projectDir/META-INF/MANIFEST.MF")
}
tasks.eclipse.dependsOn(updateManifest)

jar {
  manifest {
    attributes 'Bundle-Name': 'spotbugs-annotations',
               'Bundle-SymbolicName': 'spotbugs-annotations',
               'Bundle-Version': project.version.replace('-', '.'),
               'Export-Package': 'edu.umd.cs.findbugs.annotations',
               'Bundle-RequiredExecutionEnvironment': 'J2SE-1.5',
               'Bundle-ManifestVersion': '2'
  }
}

javadoc {
  options.with {
    memberLevel = JavadocMemberLevel.PUBLIC
    docTitle = 'SpotBugs Annotation Documentation'
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'SpotBugs Annotations'
        description 'Annotations the SpotBugs tool supports'
      }
    }
  }
}

// Module name should be reverse-DNS (com.github.spotbugs) just like package name
// http://blog.joda.org/2017/04/java-se-9-jpms-module-naming.html
ext.moduleName = 'com.github.spotbugs.annotations'
apply from: "$rootDir/gradle/jigsaw.gradle"

spotbugs {
  effort = "max"
  reportLevel = "high"
  toolVersion '3.1.0-RC6'
}

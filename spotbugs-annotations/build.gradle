apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/maven.gradle"

tasks.named('compileJava', JavaCompile).configure {
    options.release = 11
}

eclipse {
  jdt {
    sourceCompatibility = 11
    targetCompatibility = 11
    javaRuntimeName = "JavaSE-11"
  }
}

dependencies {
  api 'com.google.code.findbugs:jsr305:3.0.2'
}

def manifestSpec = java.manifest {
  attributes 'Bundle-Name': 'spotbugs-annotations',
               'Bundle-SymbolicName': 'spotbugs-annotations',
               'Bundle-Version': project.version.replace('-', '.'),
               'Export-Package': 'edu.umd.cs.findbugs.annotations',
               'Bundle-RequiredExecutionEnvironment': 'JavaSE-11',
               'Bundle-ManifestVersion': '2'
}

def updateManifest = tasks.register('updateManifest') {
    // write manifests
    manifestSpec.writeTo("$projectDir/META-INF/MANIFEST.MF")
}
tasks.eclipse.dependsOn(updateManifest)

def jar = tasks.named('jar', Jar) {
  // To keep backward compatibility, delete version number from jar name
  archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
  manifest {
    attributes 'Bundle-Name': 'spotbugs-annotations',
               'Bundle-SymbolicName': 'spotbugs-annotations',
               'Bundle-Version': project.version.replace('-', '.'),
               'Export-Package': 'edu.umd.cs.findbugs.annotations',
               'Bundle-RequiredExecutionEnvironment': 'JavaSE-11',
               'Bundle-ManifestVersion': '2'
  }
}

tasks.named('javadoc', Javadoc).configure {
  options.with {
    memberLevel = JavadocMemberLevel.PUBLIC
    docTitle = 'SpotBugs Annotation Documentation'
  }
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See https://docs.oracle.com/en/java/javase/11/tools/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)
  }
  doLast {
    logger.warn('Javadoc: "missing" warnings are disabled, see #340!')
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing.publications.maven {
  pom {
    name = 'SpotBugs Annotations'
    description = 'Annotations the SpotBugs tool supports'
  }
}

// Module name should be reverse-DNS (com.github.spotbugs) just like package name
// http://blog.joda.org/2017/04/java-se-9-jpms-module-naming.html
ext.moduleName = 'com.github.spotbugs.annotations'
apply from: "$rootDir/gradle/jigsaw.gradle"

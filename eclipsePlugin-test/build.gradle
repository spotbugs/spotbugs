apply from: "$rootDir/gradle/checkstyle.gradle"

sourceSets {
  main {
    java {
      srcDirs = ['src', 'defaultScenario']
    }
    resources {
      srcDirs = ['src']
      include '**/*.properties'
      include '**/*.xml'
    }
  }
}

dependencies {
  compile(project(':eclipsePlugin')) {
    transitive = true
  }
  compile project(path: ':eclipsePlugin', configuration: 'compileOnly')

  compile project(':test-harness')
  compile 'org.hamcrest:hamcrest-all:1.3'
  compile 'org.apache.ant:ant:1.9.4'
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
javadoc {
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)

    // TODO remove command and warning below if the doclint can be properly configured
    options.addStringOption('Xmaxwarns', '3')
  }
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

jacocoTestReport {
  additionalSourceDirs = files(project(':spotbugs').sourceSets.main.java.srcDirs)
  additionalClassDirs = files(project(':spotbugs').sourceSets.main.output.classesDirs)
}

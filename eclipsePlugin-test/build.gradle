apply from: "$rootDir/gradle/checkstyle.gradle"

def localProps = new Properties()
localProps.load(new FileInputStream("$rootDir/eclipsePlugin/local.properties"))

def eclipseRootDir = file(localProps.getProperty('eclipseRoot.dir'))
// Eclipse 4.5+ uses a different directory layout under macOS. Try to detect this first.
def eclipseExecutable = new File(eclipseRootDir, "Contents/MacOS/eclipse")
def eclipsePluginsDir = new File(eclipseRootDir, "Contents/Eclipse/plugins")
if (!eclipseExecutable.exists()) {
  // Fall back to non-macOS directory layout.
  eclipsePluginsDir = new File(eclipseRootDir, "plugins")
}

sourceSets {
  main {
    java {
      srcDirs = ['src', 'defaultScenario']
    }
    resources {
      srcDirs = ['src']
      include '**/*.properties'
      include '**/*.xml'
    }
  }
}

dependencies {
  implementation(project(':eclipsePlugin')) {
    transitive = true
  }
  // List below includes all Eclipse SDK plugins except few causing troubles.
  // TODO: it should include what is required in the MANIFEST.MF, and nothing else
  compileOnly fileTree(dir:eclipsePluginsDir, include:'**/*.jar',
          exclude:[
                  '**/datanucleus-enhancer*.jar',
                  'edu.umd.cs.findbugs.**/*.jar',
                  'org.mockito*.jar',
                  'com.github.spotbugs.**/*.jar',
                  '**/*source_*.jar'
          ])

  implementation project(':test-harness')
  implementation 'org.hamcrest:hamcrest-all:1.3'
  implementation 'org.apache.ant:ant:1.10.12'
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
javadoc {
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)

    // TODO remove command and warning below if the doclint can be properly configured
    options.addStringOption('Xmaxwarns', '3')
  }
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

java {
  withSourcesJar()
}

artifacts {
  archives sourcesJar
}

spotbugs {
  ignoreFailures = true
}

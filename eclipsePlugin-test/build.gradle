plugins {
  id "eclipse-convention"
}
apply from: "$rootDir/gradle/checkstyle.gradle"

tasks.named('compileJava', JavaCompile).configure {
  options.release = 11
}

sourceSets {
  main {
    java {
      srcDirs = ['src', 'defaultScenario']
    }
    resources {
      srcDirs = ['src']
      include '**/*.properties'
      include '**/*.xml'
    }
  }
}

dependencies {
  implementation(project(':eclipsePlugin')) {
    transitive = true
  }

  implementation project(':test-harness')
  implementation 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
  implementation 'org.apache.ant:ant:1.10.15'
}

// This disables hundreds of javadoc warnings on missing tags etc, see #340
javadoc {
  doFirst {
    // This is supposed to enable everything except "missing" but doesn't work with gradle
    // See https://docs.oracle.com/en/java/javase/11/tools/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)

    // TODO remove command and warning below if the doclint can be properly configured
    options.addStringOption('Xmaxwarns', '3')
  }
  doLast {
    logger.warn('Javadoc: too many warnings, only first 3 are shown, see #340!')
  }
}

java {
  withSourcesJar()
}

artifacts {
  archives sourcesJar
}

spotbugs {
  ignoreFailures = true
}

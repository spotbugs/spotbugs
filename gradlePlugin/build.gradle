plugins{
  id 'groovy'
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "0.9.7"
}

apply from: "$rootDir/gradle/checkstyle.gradle"

version = "1.6.0"
group = "com.github.spotbugs"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile project(':spotbugs')

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'//, 'org.spockframework:spock-core:1.0-groovy-2.4'
}

test {
  // SpotBugsPluginTest.java needs SpotBugs packages in Maven local
  dependsOn ':spotbugs-annotations:install'
  dependsOn ':spotbugs:install'
  // show test output
  testLogging.showStandardStreams = true
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'https://spotbugs.github.io/'
  vcsUrl = 'https://github.com/spotbugs/spotbugs'
  description = "Performs quality checks on your project's Java source files using SpotBugs and generates reports from these checks"
  tags = ['spotbugs', 'findbugs', 'code quality']

  plugins {
    spotbugsPlugin {
      id = 'com.github.spotbugs'
      displayName = 'SpotBugs Gradle plugin'
    }
  }
}

def spotBugsVersion = project(':spotbugs').version
task processVersionFile(type: WriteProperties) {
  outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

  property 'spotbugs-version', spotBugsVersion
}
tasks.processResources.dependsOn processVersionFile

tasks.publishPlugins.doFirst {
  if (version.endsWith('-SNAPSHOT')) {
    throw new StopExecutionException("Skip publishing Gradle plugin because its version is not stable: ${version}")
  }
  if (spotBugsVersion.endsWith('-SNAPSHOT')) {
    throw new StopExecutionException("Skip publishing Gradle plugin because SpotBugs version is not stable: ${spotBugsVersion}")
  }
}

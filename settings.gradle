def eclipseFile = "eclipsePlugin/local.properties"
if (new File(rootDir, eclipseFile).exists()) {
  include ':eclipsePlugin', ':eclipsePlugin-test', ':eclipsePlugin-junit', ':spotbugsTestCases', ':spotbugs', ':spotbugs-tests', ':test-harness', ':spotbugs-annotations', ':spotbugs-ant'
} else {
  logger.lifecycle('Eclipse plugin configuration (' + eclipseFile + ') was not found. Skipping Eclipse plugin...')
  include ':spotbugsTestCases', ':spotbugs', ':spotbugs-tests', ':test-harness', ':spotbugs-annotations', ':spotbugs-ant'
}

include ':test-harness-core', ':test-harness-jupiter'

boolean isCiServer = System.getenv().containsKey("CI")
buildCache {
  remote(HttpBuildCache) {
    url = 'https://spotbugs.kengo-toda.jp/cache/'
    push = isCiServer
    if (isCiServer) {
      print "Connecting to Gradle Build-Cache with write access..."
      credentials {
        username = 'travis'
        password = System.getenv('GRADLE_BUILD_CACHE_PASSWORD')
      }
    }
  }
}

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/maven.gradle"

// to test with JUnit 5
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
  }
}
apply plugin: 'org.junit.platform.gradle.plugin'

dependencies {
  compileOnly project(':spotbugs')
  compileOnly 'org.junit.jupiter:junit-jupiter-api:5.0.0'
  compile project(':test-harness-core')
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
  testCompile project(':spotbugs')
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0'
}

javadoc {
  options.with {
    tags = ['Test']
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'JUnit 5 Test Harness for SpotBugs Plugin'
        description 'A test harness library for SpotBugs plugin developers to test on JUnit 5'
      }
    }
  }
}

// Module name should be reverse-DNS (com.github.spotbugs) just like package name
// http://blog.joda.org/2017/04/java-se-9-jpms-module-naming.html
ext.moduleName = 'com.github.spotbugs.test.jupiter'
apply from: "$rootDir/gradle/jigsaw.gradle"

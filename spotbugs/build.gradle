apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/maven.gradle"

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java', 'src/gui']
      exclude '**/*.properties'
      exclude '**/*.db'
      exclude '**/*.html'
      exclude '**/*.png'
    }
    resources {
      /*
       * Because why put everything in a single folder when you can split it
       * and mix it with other files you don't actually need together
      */
      srcDirs = ['src/xsl', 'etc', 'src/main/java', 'src/gui']
      include '*.xsd'
      include 'bugrank.txt'
      include 'findbugs.xml'
      include 'messages*.xml'
      include '**/*.properties'
      exclude 'doc.properties'
      exclude 'script.properties'
      include '*.xsl'
      include '**/*.db'
      include '**/*.html'
      include '**/*.png'
    }
  }
}

dependencies {
  compile 'org.ow2.asm:asm-debug-all:6.0_BETA'
  compile 'net.jcip:jcip-annotations:1.0'
  compile 'dom4j:dom4j:1.6.1'
  compile 'jaxen:jaxen:1.1.6' // only transitive through dom4j:dom4j:1.6.1, which has an *optional* dependency on jaxen:jaxen.
  compile 'commons-lang:commons-lang:2.6'
  compile 'com.google.code.findbugs:jFormatString:3.0.0'

  // These annotations are repackaged to spotbugs.jar, to keep backward compatibility for Ant task.
  // If they're not repackaged, Ant task will report 'java.lang.ClassNotFoundException: edu.umd.cs.findbugs.annotations.CleanupObligation'
  compile project(':spotbugs-annotations')

  compileOnly 'com.apple:AppleJavaExtensions:1.4'

  testCompile 'junit:junit:4.12'
  testCompile 'jdepend:jdepend:2.9.1'
  testCompile 'org.apache.ant:ant:1.9.4'

  // TODO : Some of these can be extracted to actual dependencies
  compile fileTree(dir: 'lib', include: '*.jar')
}

clean {
	delete ".libs"
}

tasks.withType(Jar).all {
  includeEmptyDirs = false
  // FIXME: this is ugly, but is what ant is currently doing... our own jars are included as dependencies :S
  //destinationDir = file("$projectDir/lib")
}


eclipse.classpath.file {
    whenMerged {
	    classpath ->
	       classpath.entries.removeAll { entry -> entry.kind == 'lib' && entry.path.contains('xml-apis')}
	       classpath.entries.forEach {
	           entry ->
		           if(entry.kind == 'lib' && !entry.path.contains('spotbugs-annotations')) {
		               entry.path = ".libs/" + java.nio.file.Paths.get(entry.path).getFileName().toString()
		               entry.exported = true
		           }
	    }
    }
}

task copyLibsForEclipse (type: Copy) {
    from configurations.compile.files
    into ".libs"
    include "*.jar"
    exclude "*xml-apis*.jar"
    exclude "*spotbugs-annotations*.jar"


    from configurations.testCompile.files
    into ".libs"
    include "*.jar"

    from configurations.compileOnly.files
    into ".libs"
    include "*.jar"
}

task updateManifest {
  dependsOn configurations.compile, copyLibsForEclipse
  doLast {
    def manifestSpec = manifest {
      from "$projectDir/META-INF/MANIFEST-TEMPLATE.MF"
      attributes 'Main-Class': 'edu.umd.cs.findbugs.LaunchAppropriateUI',
               'Bundle-Version': project.version.replace('-', '.'),
               'Bundle-ClassPath': 'spotbugs.jar,' +  fileTree(dir: '.libs').collect { projectDir.toPath().relativize(it.toPath()).toString() }.join(',')
    }
    // write manifests
    manifestSpec.writeTo("$projectDir/META-INF/MANIFEST.MF")
  }
}

// Manually define what goes into the default jar, since it's not only main sourceset
jar {
  from sourceSets.main.output

  // Workaround for missing released BCEL maven artifact, see https://github.com/spotbugs/spotbugs/issues/327
  // Should be removed after #327 is fixed.
  from zipTree("$projectDir/lib/bcel-6.1-20161207.023659-9.jar").matching {
    exclude 'META-INF/**'
  }

  manifest {
    attributes 'Main-Class': 'edu.umd.cs.findbugs.LaunchAppropriateUI',
               'Bundle-Version': project.version,
               'Class-Path': 'spotbugs-annotations.jar dom4j-1.6.1.jar jaxen-1.1.6.jar asm-debug-all-6.0_BETA.jar jsr305-3.0.1.jar jFormatString-3.0.0.jar commons-lang-2.6.jar'
  }
}

// Populate bin folder with scripts
task scripts(type:Copy) {
  // Take the properties into account for up-to-date checking
  inputs.file "$projectDir/etc/script.properties"

  def props = new Properties()
  doFirst {
    props.load(new FileInputStream("$projectDir/etc/script.properties"))

    filesNotMatching('**/*.ico') {
      filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        'GET_FBHOME': props.getProperty('script.get.fbhome'),
        'SET_DEFAULT_JAVA': props.getProperty('script.set.default.java'),
        'WRAP_JAVA': props.getProperty('script.wrap.java'),
        'WRAP_JAR': props.getProperty('script.wrap.jar'),
        'DEFINE_ESCAPE_ARG': props.getProperty('script.define.escape_arg')])
      fileMode 0755
    }
  }

  from 'src/scripts/standard'
  from 'src/scripts/windows'
  from('src/scripts/deprecated') {
    into 'deprecated'
  }
  from('src/scripts/experimental') {
    into 'experimental'
  }

  destinationDir file("$buildDir/bin")
  duplicatesStrategy DuplicatesStrategy.INCLUDE
}

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

apply plugin: 'distribution'
distributions {
  main {
    contents {
      from(scripts) {
        into 'bin'
      }
      from 'README.txt'
      from ('plugin') {
         into 'plugin'
         include 'README'
      }
      from 'licenses'
      from(configurations.compile) {
        into 'lib'
        include '**/*.jar'
        rename "(.*)-${project.version}.jar", '$1.jar'
      }
      from([jar, project(':spotbugs-ant').jar]) {
        into 'lib'
        rename "(.*)-${project.version}.jar", '$1.jar'
      }
      from('src/xsl') {
        into 'src/xsl'
        include '**/*.xsl'
      }
    }
  }
}

distTar.compression = Compression.GZIP
tasks['assemble'].dependsOn tasks['assembleDist']

task distSrcZip(type:Exec) {
  commandLine 'git', 'archive', '-o', "${buildDir}/distributions/spotbugs-${project.version}-source.zip",
    '--prefix', "spotbugs-${project.version}/", 'HEAD'
}
distSrcZip.onlyIf {
  file("$rootDir/.git").isDirectory()
}
tasks['assembleDist'].finalizedBy distSrcZip

test {
  dependsOn ':spotbugsTestCases:build'
  maxHeapSize = '1G'
}

task unzipDist(type:Copy, dependsOn:distZip) {
  from zipTree(distZip.outputs.files.singleFile)
  into file("$buildDir/smoketest/")
  // Remove prefix
  eachFile { details ->
    details.path = details.path - "spotbugs-${project.version}"
  }
}

// TODO : actually check the output beyond it not blowing up in pieces?
task smokeTest {
  dependsOn unzipDist, project(':spotbugs-ant').jar
  doLast {
    ant.taskdef(name:'spotbugs', classname:'edu.umd.cs.findbugs.anttask.FindBugsTask', classpath:project(':spotbugs-ant').jar.outputs.files.asPath)
    ant.spotbugs(home:"$buildDir/smoketest/", output:'xml:withMessages',
              jvmargs:'-ea -Xmx1200m',
              excludeFilter:'findbugsExclude.xml',
              projectName:'spotbugs',
              maxRank:'20',
              timeout:'1800000',
              outputFile:"${buildDir}/smoketest/findbugscheckAll.xml") {
      sourcePath(path:'src/main/java:src/gui:src/tools')
      'class'(location:project.tasks['compileJava'].destinationDir)
      configurations.compile.each { File file -> auxClasspath(path:file.path) }
    }
  }
}

coveralls {
  jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'SpotBugs'
        description "SpotBugs: Because it's easy!"
      }
    }
  }
}

ant.importBuild('build.xml') { antTargetName ->
  'ant-' + antTargetName
}

// TODO : jsr305.jar (really?)
// TODO : generatemanual (we should decide what to do with the manual)
// TODO : generatepdfmanual
// TODO : bugdesc

// Smoke testing
// TODO : plugincheck_fbcontrib
// TODO : spotbugsTestCases (already analyzed by unit tests... not particularly useful, is it?)

// Integration testing
// TODO : validatesamplehtmloutput (really?)
// TODO : validatesamplexmloutput

// XML validation
// TODO : validate
